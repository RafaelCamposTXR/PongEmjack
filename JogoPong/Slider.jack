// 'Slider' é a barrinha que vai tentar evitar que a bola acerte o chao

class Slider {

    // define-se local na tela
    field int x, y;

    // largura e altura
    field int largura, altura;

    // a direção que a slider irá é baseada em inteiros
    // 1 para esquerda, 2 para direita
    field int direcao;

    // Enfim, a slider é construída abaixo, com as especificações dadas
    constructor Slider new(int xR, int yR, int largR, int alturaR) {
      let x = xR;
      let y = yR;
      let largura = largR;
      let altura = alturaR;
      let direcao = 2;

      do mostrar();  // Exibe a slider na tela

      return this;   // Retorna a instância da classe
    }

    // desocupa o local de memória do objeto
    method void LimpaMemoria() {
      do Memory.deAlloc(this);
      return;
    }

    // desenha a slider na tela
    method void mostrar() {
      do Screen.setColor(true);
	    do desenhar();
      return;
    }

    // limpa a slider da tela
    method void limpar() {
      do Screen.setColor(false);
	    do desenhar();
      return;
    }

    // desenha a slider na tela
    method void desenhar() {
	    do Screen.drawRectangle(x, y, x + largura, y + altura);
	    return;
    }

    // determina a direção da slider
    // Para 0, não se mexe
    // Para 1, esquerda
    // Para 2, direita
    method void setDirecao(int direcA) {
	    let direcao = direcA;
      return;
    }

    // retorna a localização do ponto esquerdo da slider
    method int LadoEsquerdo() {
        return x;
    }

    // retorna a localização do ponto esquerda da slider
    method int LadoDireito() {
        return x + largura;
    }

    // determina a largura da slider
    method void setLargura(int largR) {
      do limpar();
	    let largura = largR;   // Atualiza a largura da slider
      do mostrar();
      return;
    }

    // movimentação da slider
    method void move() {

      // esquerda
	    if (direcao = 1) {

        // a coordenada é levada à esquerda
        let x = x - 4;

        // a slider não deve ultrapassar os limites da tela
        if (x < 0) {
          let x = 0;
        }

	// Limpa a área anterior da slider na tela
        do Screen.setColor(false);
        do Screen.drawRectangle((x + largura) + 1, y, (x + largura) + 4, y + altura);

        // Exibe a slider na nova posição na tela
        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + 3, y + altura);
	    }

        // direita
        else {

            // a coordenada é levada à direita
            let x = x + 4;

            // a slider não deve ultrapassar os limites da tela
            if ((x + largura) > 511) {
                let x = 511 - largura;
            }
	    // Limpa a área anterior da slider na tela
            do Screen.setColor(false);
            do Screen.drawRectangle(x - 4, y, x - 1, y + altura);

	    // Exibe a slider na nova posição na tela
            do Screen.setColor(true);
            do Screen.drawRectangle((x + largura) - 3, y, x + largura, y + altura);
        }

        return;
    }
}
