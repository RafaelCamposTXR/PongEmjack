// O jogo do Pong precisa de uma 'raquete' com configurações
// de localização na tela, largura, altura, movimentação para
// ambos lados, ...

class Slider {

    // define-se local na tela
    field int x, y;

    // largura e altura
    field int largura, altura;

    // a direção que a raquete irá é baseada em inteiros
    // 1 para esquerda, 2 para direita
    field int direcao;

    // Enfim, a raquete é construída abaixo, com as especificações dadas
    constructor Slider new(int xR, int yR, int largR, int alturaR) {
	let x = xR;
	let y = yR;
	let largura = largR;
	let altura = alturaR;
	let direcao = 2;

        do mostrar();

        return this;
    }

    // desocupa o local de memória do objeto
    method void LimpaMemoria() {
        do Memory.deAlloc(this);
        return;
    }

    // desenha a raquete na tela
    method void mostrar() {
        do Screen.setColor(true);
	do desenhar();
        return;
    }

    // limpa a raquete da tela
    method void limpar() {
        do Screen.setColor(false);
	do desenhar();
        return;
    }

    // desenha a raquete na tela
    method void desenhar() {
	do Screen.drawRectangle(x, y, x + largura, y + altura);
	return;
    }

    // determina a direção da raquete
    // Para 0, não se mexe
    // Para 1, esquerda
    // Para 2, direita
    method void setDirecao(int direcA) {
	let direcao = direcA;
        return;
    }

    // retorna a localização do ponto esquerda da raquete
    method int LadoEsquerdo() {
        return x;
    }

    // retorna a localização do ponto esquerda da raquete
    method int LadoDireito() {
        return x + largura;
    }

    // determina a largura da raquete
    method void setLargura(int largR) {
        do limpar();
	let largura = largR;
        do mostrar();
        return;
    }

    // movimentação da raquete
    method void move() {

        // esquerda
	    if (direcao = 1) {

            // a coordenada é levada à esquerda
            let x = x - 4;

            // a raquete não deve ultrapassar os limites da tela
            if (x < 0) {
                let x = 0;
            }
            do Screen.setColor(false);
            do Screen.drawRectangle((x + largura) + 1, y, (x + largura) + 4, y + altura);
            do Screen.setColor(true);
            do Screen.drawRectangle(x, y, x + 3, y + altura);
	    }

        // direita
        else {

            // a coordenada é levada à direita
            let x = x + 4;

            // a raquete não deve ultrapassar os limites da tela
            if ((x + largura) > 511) {
                let x = 511 - largura;
            }
            do Screen.setColor(false);
            do Screen.drawRectangle(x - 4, y, x - 1, y + altura);
            do Screen.setColor(true);
            do Screen.drawRectangle((x + largura) - 3, y, x + largura, y + altura);
        }

        return;
    }
}
